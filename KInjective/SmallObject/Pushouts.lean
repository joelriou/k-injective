/-
Copyright (c) 2024 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks
import Mathlib.CategoryTheory.Limits.Shapes.Products
import KInjective.SmallObject.Lifting

/-!
# Construction for the small object argument

Given a family of morphisms `f i : A i âŸ¶ B i` in a category `C`
and an object `S : C`, we define a functor
`SmallObject.functor f S : Over S â¥¤ Over S` which sends
an object given by `Ï€X : X âŸ¶ S` to the pushout `functorObj f S Ï€X`:
```
âˆ (functorObjSrcFamily f Ï€X) âŸ¶      X

|                                   |
|                                   |
v                                   v

âˆ (functorObjTgtFamily f Ï€X) âŸ¶ functorObj f S Ï€X
```
where the morphism on the left is a coproduct (of copies of maps `f i`)
indexed by a type `FunctorObjIndex f Ï€X` which parametrizes the
diagrams of the form
```
A i âŸ¶ X
 |    |
 |    |
 v    v
B i âŸ¶ S
```

The morphism `Î¹FunctorObj f S Ï€X : X âŸ¶ functorObj f S Ï€X` is part of
a natural transformation `SmallObject.Îµ f S : ðŸ­ (Over S) âŸ¶ functor f S`.
The main idea in this construction is that for any commutative square
as above, there may not exist a lifting `B i âŸ¶ X`, but the construction
provides a tautological morphism `B i âŸ¶ functorObj f S Ï€X`
(see `SmallObject.Î¹FunctorObj_extension`).

## TODO

* Show that `Î¹FunctorObj f S Ï€X : X âŸ¶ functorObj f S Ï€X` has the
left lifting property with respect to the class of morphisms that
have the right lifting property with respect to the morphisms `f i`.

## References
- https://ncatlab.org/nlab/show/small+object+argument

-/
universe w v u

namespace CategoryTheory

open Category Limits

namespace SmallObject

variable {C : Type u} [Category.{v} C] {I : Type w} {A B : I â†’ C} (f : âˆ€ i, A i âŸ¶ B i)

section

variable {S : C} {X Y Z : C} (Ï€X : X âŸ¶ S) (Ï€Y : Y âŸ¶ S) (Ï€Z : Z âŸ¶ S)
  (Ï† : X âŸ¶ Y) (hÏ† : Ï† â‰« Ï€Y = Ï€X) (Ïˆ : Y âŸ¶ Z) (hÏˆ : Ïˆ â‰« Ï€Z = Ï€Y)

/-- Given a family of morphisms `f i : A i âŸ¶ B i` and a morphism `Ï€X : X âŸ¶ S`,
this type parametrizes the commutative squares with a morphism `f i` on the left
and `Ï€X` in the right. -/
structure FunctorObjIndex where
  /-- an element in the index type -/
  i : I
  /-- the top morphism in the square -/
  t : A i âŸ¶ X
  /-- the bottom morphism in the square -/
  b : B i âŸ¶ S
  w : t â‰« Ï€X = f i â‰« b

attribute [reassoc (attr := simp)] FunctorObjIndex.w

variable [HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€X)) C]
  [HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€Y)) C]
  [HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€Z)) C]

/-- The family of objects `A x.i` parametrized by `x : FunctorObjIndex f Ï€X`. -/
abbrev functorObjSrcFamily (x : FunctorObjIndex f Ï€X) : C := A x.i

/-- The family of objects `B x.i` parametrized by `x : FunctorObjIndex f Ï€X`. -/
abbrev functorObjTgtFamily (x : FunctorObjIndex f Ï€X) : C := B x.i

/-- The family of the morphisms `f x.i : A x.i âŸ¶ B x.i`
parametrized by `x : FunctorObjIndex f Ï€X`. -/
abbrev functorObjLeftFamily (x : FunctorObjIndex f Ï€X) :
    functorObjSrcFamily f Ï€X x âŸ¶ functorObjTgtFamily f Ï€X x := f x.i

/-- The top morphism in the pushout square in the definition of `pushoutObj f Ï€X`. -/
noncomputable abbrev functorObjTop :
    âˆ (functorObjSrcFamily f Ï€X) âŸ¶ X :=
  Limits.Sigma.desc (fun x => x.t)

/-- The left morphism in the pushout square in the definition of `pushoutObj f Ï€X`. -/
noncomputable abbrev functorObjLeft :
    âˆ (functorObjSrcFamily f Ï€X) âŸ¶ âˆ (functorObjTgtFamily f Ï€X) :=
  Limits.Sigma.map (functorObjLeftFamily f Ï€X)

variable [HasPushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X)]
  [HasPushout (functorObjTop f Ï€Y) (functorObjLeft f Ï€Y)]
  [HasPushout (functorObjTop f Ï€Z) (functorObjLeft f Ï€Z)]

/-- The functor `SmallObject.functor f S : Over S â¥¤ Over S` that is part of
the small object argument for a family of morphisms `f`, on an object given
as a morphism `Ï€X : X âŸ¶ S`. -/
noncomputable abbrev functorObj : C :=
  pushout (functorObjTop f Ï€X) (functorObjLeft f Ï€X)

/-- The canonical morphism `X âŸ¶ functorObj f Ï€X`. -/
noncomputable abbrev Î¹FunctorObj : X âŸ¶ functorObj f Ï€X := pushout.inl

/-- The canonical morphism `âˆ (functorObjTgtFamily f Ï€X) âŸ¶ functorObj f Ï€X`. -/
noncomputable abbrev ÏFunctorObj : âˆ (functorObjTgtFamily f Ï€X) âŸ¶ functorObj f Ï€X := pushout.inr

@[reassoc]
lemma functorObj_comm :
    functorObjTop f Ï€X â‰« Î¹FunctorObj f Ï€X = functorObjLeft f Ï€X â‰« ÏFunctorObj f Ï€X := pushout.condition

@[reassoc]
lemma FunctorObjIndex.comm (x : FunctorObjIndex f Ï€X) :
    f x.i â‰« Sigma.Î¹ (functorObjTgtFamily f Ï€X) x â‰« ÏFunctorObj f Ï€X = x.t â‰« Î¹FunctorObj f Ï€X := by
  simpa using (Sigma.Î¹ (functorObjSrcFamily f Ï€X) x â‰«= functorObj_comm f Ï€X).symm

/-- The canonical projection on the base object. -/
noncomputable abbrev Ï€'FunctorObj : âˆ (functorObjTgtFamily f Ï€X) âŸ¶ S := Sigma.desc (fun x => x.b)

/-- The canonical projection on the base object. -/
noncomputable def Ï€FunctorObj : functorObj f Ï€X âŸ¶ S :=
  pushout.desc Ï€X (Ï€'FunctorObj f Ï€X) (by ext; simp [Ï€'FunctorObj])

@[reassoc (attr := simp)]
lemma ÏFunctorObj_Ï€ : ÏFunctorObj f Ï€X â‰« Ï€FunctorObj f Ï€X = Ï€'FunctorObj f Ï€X := by
  simp [Ï€FunctorObj]

@[reassoc (attr := simp)]
lemma Î¹FunctorObj_Ï€FunctorObj : Î¹FunctorObj f Ï€X â‰« Ï€FunctorObj f Ï€X = Ï€X := by
  simp [Î¹FunctorObj, Ï€FunctorObj]

/-- The canonical morphism `âˆ (functorObjSrcFamily f Ï€X) âŸ¶ âˆ (functorObjSrcFamily f Ï€Y)`
induced by a morphism in `Ï† : X âŸ¶ Y` such that `Ï† â‰« Ï€X = Ï€Y`. -/
noncomputable def functorMapSrc :
    âˆ (functorObjSrcFamily f Ï€X) âŸ¶ âˆ (functorObjSrcFamily f Ï€Y) :=
  Sigma.map' (fun x => FunctorObjIndex.mk x.i (x.t â‰« Ï†) x.b (by simp [hÏ†])) (fun _ => ðŸ™ _)

@[reassoc]
lemma Î¹_functorMapSrc (i : I) (t : A i âŸ¶ X) (b : B i âŸ¶ S) (w : t â‰« Ï€X = f i â‰« b)
    (t' : A i âŸ¶ Y) (fac : t â‰« Ï† = t') :
    Sigma.Î¹ _ (FunctorObjIndex.mk i t b w) â‰« functorMapSrc f Ï€X Ï€Y Ï† hÏ† =
      Sigma.Î¹ (functorObjSrcFamily f Ï€Y) (FunctorObjIndex.mk i t' b (by rw [â† w, â† fac, assoc, hÏ†])) := by
  subst fac
  erw [Sigma.Î¹_comp_map', id_comp]

@[reassoc (attr := simp)]
lemma functorMapSrc_functorObjTop :
    functorMapSrc f Ï€X Ï€Y Ï† hÏ† â‰« functorObjTop f Ï€Y = functorObjTop f Ï€X â‰« Ï† := by
  ext âŸ¨i, t, b, wâŸ©
  simp [Î¹_functorMapSrc_assoc f Ï€X Ï€Y Ï† hÏ† i t b w _ rfl]

/-- The canonical morphism `âˆ (functorObjTgtFamily f Ï€X) âŸ¶ âˆ (functorObjTgtFamily f Ï€Y)`
induced by a morphism in `Ï† : X âŸ¶ Y` such that `Ï† â‰« Ï€X = Ï€Y`. -/
noncomputable def functorMapTgt :
    âˆ (functorObjTgtFamily f Ï€X) âŸ¶ âˆ (functorObjTgtFamily f Ï€Y) :=
  Sigma.map' (fun x => FunctorObjIndex.mk x.i (x.t â‰« Ï†) x.b (by simp [hÏ†])) (fun _ => ðŸ™ _)

@[reassoc]
lemma Î¹_functorMapTgt (i : I) (t : A i âŸ¶ X) (b : B i âŸ¶ S) (w : t â‰« Ï€X = f i â‰« b)
    (t' : A i âŸ¶ Y) (fac : t â‰« Ï† = t') :
    Sigma.Î¹ _ (FunctorObjIndex.mk i t b w) â‰« functorMapTgt f Ï€X Ï€Y Ï† hÏ† =
      Sigma.Î¹ (functorObjTgtFamily f Ï€Y) (FunctorObjIndex.mk i t' b (by rw [â† w, â† fac, assoc, hÏ†])) := by
  subst fac
  erw [Sigma.Î¹_comp_map', id_comp]

lemma functorMap_comm :
    functorObjLeft f Ï€X â‰« functorMapTgt f Ï€X Ï€Y Ï† hÏ† =
      functorMapSrc f Ï€X Ï€Y Ï† hÏ† â‰« functorObjLeft f Ï€Y := by
  ext âŸ¨i, t, b, wâŸ©
  simp only [Î¹_colimMap_assoc, Discrete.natTrans_app, Î¹_colimMap,
    Î¹_functorMapTgt f Ï€X Ï€Y Ï† hÏ† i t b w _ rfl,
    Î¹_functorMapSrc_assoc f Ï€X Ï€Y Ï† hÏ† i t b w _ rfl]

/-- The functor `SmallObject.functor f S : Over S â¥¤ Over S` that is part of
the small object argument for a family of morphisms `f`, on morphisms. -/
noncomputable def functorMap : functorObj f Ï€X âŸ¶ functorObj f Ï€Y :=
  pushout.map _ _ _ _ Ï† (functorMapTgt f Ï€X Ï€Y Ï† hÏ†) (functorMapSrc f Ï€X Ï€Y Ï† hÏ†) (by simp)
    (functorMap_comm f Ï€X Ï€Y Ï† hÏ†)

@[reassoc (attr := simp)]
lemma functorMap_Ï€ : functorMap f Ï€X Ï€Y Ï† hÏ† â‰« Ï€FunctorObj f Ï€Y = Ï€FunctorObj f Ï€X := by
  ext âŸ¨i, t, b, wâŸ©
  Â· simp [functorMap, hÏ†]
  Â· simp [functorMap, Î¹_functorMapTgt_assoc f Ï€X Ï€Y Ï† hÏ† i t b w _ rfl]

variable (X) in
@[simp]
lemma functorMap_id : functorMap f Ï€X Ï€X (ðŸ™ X) (by simp) = ðŸ™ _ := by
  ext âŸ¨i, t, b, wâŸ©
  Â· simp [functorMap]
  Â· simp [functorMap, Î¹_functorMapTgt_assoc f Ï€X Ï€X (ðŸ™ X) (by simp) i t b w t (by simp)]


@[reassoc (attr := simp)]
lemma Î¹FunctorObj_naturality :
    Î¹FunctorObj f Ï€X â‰« functorMap f Ï€X Ï€Y Ï† hÏ† = Ï† â‰« Î¹FunctorObj f Ï€Y := by
  simp [Î¹FunctorObj, functorMap]

end

variable (S : C) [HasPushouts C]
  [âˆ€ {X : C} (Ï€X : X âŸ¶ S), HasColimitsOfShape (Discrete (FunctorObjIndex f Ï€X)) C]

/-- The functor `Over S â¥¤ Over S` that is constructed in order to apply the small
object argument to a family of morphisms `f i : A i âŸ¶ B i`, see the introduction
of the file `Mathlib.CategoryTheory.SmallObject.Construction` -/
@[simps! obj map]
noncomputable def functor : Over S â¥¤ Over S where
  obj Ï€ := Over.mk (Ï€FunctorObj f Ï€.hom)
  map {Ï€â‚ Ï€â‚‚} Ï† := Over.homMk (functorMap f Ï€â‚.hom Ï€â‚‚.hom Ï†.left (Over.w Ï†))
  map_id _ := by ext; dsimp; simp
  map_comp {Ï€â‚ Ï€â‚‚ Ï€â‚ƒ} Ï† Ï†' := by
    ext1
    dsimp
    ext âŸ¨i, t, b, wâŸ©
    Â· simp
    Â· simp [functorMap, Î¹_functorMapTgt_assoc f Ï€â‚.hom Ï€â‚‚.hom Ï†.left (Over.w Ï†) i t b w _ rfl,
        Î¹_functorMapTgt_assoc f Ï€â‚.hom Ï€â‚ƒ.hom (Ï†.left â‰« Ï†'.left) (Over.w (Ï† â‰« Ï†')) i t b w _ rfl,
        Î¹_functorMapTgt_assoc f Ï€â‚‚.hom Ï€â‚ƒ.hom (Ï†'.left) (Over.w Ï†') i (t â‰« Ï†.left) b
          (by simp [w]) (t â‰« Ï†.left â‰« Ï†'.left) (by simp)]

/-- The canonical natural transformation `ðŸ­ (Over S) âŸ¶ functor f S`. -/
@[simps! app]
noncomputable def Îµ : ðŸ­ (Over S) âŸ¶ functor f S where
  app w := Over.homMk (Î¹FunctorObj f w.hom)

variable {S}

lemma Î¹FunctorObj_extension {X : C} (Ï€X : X âŸ¶ S) {i : I} (t : A i âŸ¶ X) (b : B i âŸ¶ S)
    (sq : CommSq t (f i) Ï€X b) :
    âˆƒ (l : B i âŸ¶ functorObj f Ï€X), f i â‰« l = t â‰« Î¹FunctorObj f Ï€X âˆ§
      l â‰« Ï€FunctorObj f Ï€X = b :=
  âŸ¨Sigma.Î¹ (functorObjTgtFamily f Ï€X) (FunctorObjIndex.mk i t b sq.w) â‰«
    ÏFunctorObj f Ï€X, (FunctorObjIndex.mk i t b _).comm, by simpâŸ©

lemma Îµ_extension {i : I} (b : B i âŸ¶ S) {Z : Over S} (t : Over.mk (f i â‰« b) âŸ¶ Z) :
    âˆƒ (l : Over.mk b âŸ¶ (functor f S).obj Z), t â‰« (Îµ f S).app Z = (by exact Over.homMk (f i)) â‰« l := by
  obtain âŸ¨l, facâ‚, facâ‚‚âŸ© := Î¹FunctorObj_extension f Z.hom _ _ (CommSq.mk (Over.w t))
  exact âŸ¨Over.homMk l facâ‚‚, by ext; exact facâ‚.symmâŸ©

section

variable {X : C} (Ï€X : X âŸ¶ S)

lemma functorObjLeft_mem_rlpWith_llpWith :
    (MorphismProperty.ofHoms f).rlpWith.llpWith (functorObjLeft f Ï€X) := by
  intro Z T p hp
  have pif := fun i => hp (f i) âŸ¨iâŸ©
  dsimp [functorObjLeft]
  infer_instance

lemma Î¹FunctorObj_mem_rlpWith_llpWith  :
    (MorphismProperty.ofHoms f).rlpWith.llpWith (Î¹FunctorObj f Ï€X) := by
  intro Z T p hp
  have pif := functorObjLeft_mem_rlpWith_llpWith f Ï€X p hp
  infer_instance

end

end SmallObject

end CategoryTheory
