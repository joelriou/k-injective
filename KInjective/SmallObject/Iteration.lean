import KInjective.SmallObject.WellOrderContinuous

universe u

lemma monotone_inclusion_lt_le_of_le {Œ± : Type*} [Preorder Œ±] {k j : Œ±} (hkj : k ‚â§ j) :
    Monotone (fun ‚ü®i, hi‚ü© => ‚ü®i, hi.le.trans hkj‚ü© : { i | i < k } ‚Üí { i | i ‚â§ j}) :=
  fun _ _ h => h

lemma monotone_inclusion_le_le_of_le {Œ± : Type*} [Preorder Œ±] {k j : Œ±} (hkj : k ‚â§ j) :
    Monotone (fun ‚ü®i, hi‚ü© => ‚ü®i, hi.trans hkj‚ü© : { i | i ‚â§ k } ‚Üí { i | i ‚â§ j}) :=
  fun _ _ h => h

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category C]
  {Œ¶ : C ‚•§ C} (Œµ : ùü≠ C ‚ü∂ Œ¶)
  {J : Type u} [LinearOrder J] [IsWellOrder J (¬∑ < ¬∑)] [OrderBot J]

namespace Functor

namespace Iteration

variable {j : J} (F : { i | i ‚â§ j } ‚•§ C ‚•§ C)

section

variable {i : J} (hi : i ‚â§ j)

def restrictionLT : { k | k < i } ‚•§ C ‚•§ C :=
  (monotone_inclusion_lt_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLT_obj (k : J) (hk : k < i) :
    (restrictionLT F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.le.trans hi‚ü© := rfl

@[simp]
lemma restrictionLT_map {k‚ÇÅ k‚ÇÇ : { k | k < i }} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLT F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

@[simps]
def coconeOfLE :
    Cocone (restrictionLT F hi) where
  pt := F.obj ‚ü®i, hi‚ü©
  Œπ :=
    { app := fun ‚ü®k, hk‚ü© => F.map (homOfLE (by simpa using hk.le))
      naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© _ => by
        dsimp
        rw [comp_id, ‚Üê Functor.map_comp, homOfLE_comp] }

def restrictionLE : { k | k ‚â§ i } ‚•§ C ‚•§ C :=
  (monotone_inclusion_le_le_of_le hi).functor ‚ãô F

@[simp]
lemma restrictionLE_obj (k : J) (hk : k ‚â§ i) :
    (restrictionLE F hi).obj ‚ü®k, hk‚ü© = F.obj ‚ü®k, hk.trans hi‚ü© := rfl

@[simp]
lemma restrictionLE_map {k‚ÇÅ k‚ÇÇ : { k | k ‚â§ i }} (œÜ : k‚ÇÅ ‚ü∂ k‚ÇÇ) :
    (restrictionLE F hi).map œÜ = F.map (homOfLE (by simpa using leOfHom œÜ)) := rfl

end

noncomputable abbrev mapSucc' (i : J) (hi : i < j) :
    F.obj ‚ü®i, hi.le‚ü© ‚ü∂ F.obj ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© :=
  F.map (homOfLE (by simpa only [Subtype.mk_le_mk] using self_le_wellOrderSucc i))

end Iteration

structure Iteration (j : J) where
  F : { i | i ‚â§ j } ‚•§ C ‚•§ C
  isoZero : F.obj ‚ü®‚ä•, bot_le‚ü© ‚âÖ ùü≠ C
  isoSucc (i : J) (hi : i < j) :
    F.obj ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© ‚âÖ F.obj ‚ü®i, hi.le‚ü© ‚ãô Œ¶
  mapSucc'_eq (i : J) (hi : i < j) :
    Iteration.mapSucc' F i hi = whiskerLeft _ Œµ ‚â´ (isoSucc i hi).inv
  isColimit (i : J) [IsWellOrderLimitElement i] (hi : i ‚â§ j) :
    IsColimit (Iteration.coconeOfLE F hi)

namespace Iteration

variable {Œµ}
variable {j : J} (iter‚ÇÅ iter‚ÇÇ : Œ¶.Iteration Œµ j)

noncomputable abbrev mapSucc (i : J) (hi : i < j) :
    iter‚ÇÅ.F.obj ‚ü®i, hi.le‚ü© ‚ü∂ iter‚ÇÅ.F.obj ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© :=
  mapSucc' iter‚ÇÅ.F i hi

lemma mapSucc_eq (i : J) (hi : i < j) :
    iter‚ÇÅ.mapSucc i hi = whiskerLeft _ Œµ ‚â´ (iter‚ÇÅ.isoSucc i hi).inv :=
  iter‚ÇÅ.mapSucc'_eq _ hi

structure Hom where
  natTrans : iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F
  natTrans_app_zero : natTrans.app ‚ü®‚ä•, bot_le‚ü© = iter‚ÇÅ.isoZero.hom ‚â´ iter‚ÇÇ.isoZero.inv := by aesop_cat
  natTrans_app_succ (i : J) (hi : i < j) :
    natTrans.app ‚ü®wellOrderSucc i, wellOrderSucc_le hi‚ü© = (iter‚ÇÅ.isoSucc i hi).hom ‚â´
      whiskerRight (natTrans.app ‚ü®i, hi.le‚ü©) _ ‚â´ (iter‚ÇÇ.isoSucc i hi).inv := by aesop_cat

namespace Hom

attribute [simp] natTrans_app_zero

@[simps]
def id : Hom iter‚ÇÅ iter‚ÇÅ where
  natTrans := ùüô _

variable {iter‚ÇÅ iter‚ÇÇ}

lemma ext' {f g : Hom iter‚ÇÅ iter‚ÇÇ} (h : f.natTrans = g.natTrans) : f = g := by
  cases f
  cases g
  subst h
  rfl

attribute [local ext] ext'

@[simps]
def comp {iter‚ÇÉ : Iteration Œµ j} (f : Hom iter‚ÇÅ iter‚ÇÇ) (g : Hom iter‚ÇÇ iter‚ÇÉ) : Hom iter‚ÇÅ iter‚ÇÉ where
  natTrans := f.natTrans ‚â´ g.natTrans
  natTrans_app_succ i hi := by simp [natTrans_app_succ _ _ hi]

instance : Category (Iteration Œµ j) where
  Hom := Hom
  id := id
  comp := comp

instance : Subsingleton (iter‚ÇÅ ‚ü∂ iter‚ÇÇ) where
  allEq f g := ext' (by
    let P := fun (i : J) => ‚àÄ (hi : i ‚â§ j), f.natTrans.app ‚ü®i, hi‚ü© = g.natTrans.app ‚ü®i, hi‚ü©
    suffices ‚àÄ (i : J), P i by
      ext ‚ü®i, hi‚ü© : 2
      apply this
    refine fun _ => WellFoundedLT.induction _ (fun i hi hi' => ?_)
    obtain rfl|‚ü®i, rfl, hi''‚ü©|_ := eq_bot_or_eq_succ_or_isWellOrderLimitElement i
    ¬∑ simp only [natTrans_app_zero]
    ¬∑ simp only [Hom.natTrans_app_succ _ i (lt_of_lt_of_le hi'' hi'), hi i hi'']
    ¬∑ exact (iter‚ÇÅ.isColimit i hi').hom_ext (fun ‚ü®k, hk‚ü© => by simp [hi k hk]))

end Hom

variable {iter‚ÇÅ iter‚ÇÇ} in
@[reassoc]
lemma natTrans_naturality (œÜ : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) (i‚ÇÅ i‚ÇÇ : J) (h : i‚ÇÅ ‚â§ i‚ÇÇ) (h' : i‚ÇÇ ‚â§ j) :
    iter‚ÇÅ.F.map (by exact homOfLE h) ‚â´ œÜ.natTrans.app ‚ü®i‚ÇÇ, h'‚ü© =
      œÜ.natTrans.app ‚ü®i‚ÇÅ, h.trans h'‚ü© ‚â´ iter‚ÇÇ.F.map (by exact homOfLE h) := by
  apply œÜ.natTrans.naturality

@[simps F isoZero isoSucc]
def trunc {i : J} (hi : i ‚â§ j) : Iteration Œµ i where
  F := restrictionLE (iter‚ÇÅ.F) hi
  isoZero := iter‚ÇÅ.isoZero
  isoSucc k hk := iter‚ÇÅ.isoSucc k (lt_of_lt_of_le hk hi)
  mapSucc'_eq k hk := iter‚ÇÅ.mapSucc'_eq k (lt_of_lt_of_le hk hi)
  isColimit k _ hk := iter‚ÇÅ.isColimit k (hk.trans hi)

variable (Œµ) in
@[simps obj]
def truncFunctor {i : J} (hi : i ‚â§ j) : Iteration Œµ j ‚•§ Iteration Œµ i where
  obj iter := iter.trunc hi
  map {iter‚ÇÅ iter‚ÇÇ} œÜ :=
    { natTrans := whiskerLeft _ œÜ.natTrans
      natTrans_app_succ := fun k hk => œÜ.natTrans_app_succ k (lt_of_lt_of_le hk hi) }

variable {iter‚ÇÅ iter‚ÇÇ}

@[simp]
lemma truncFunctor_map_natTrans_app
    (œÜ : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) {i : J} (hi : i ‚â§ j) (k : J) (hk : k ‚â§ i) :
    ((truncFunctor Œµ hi).map œÜ).natTrans.app ‚ü®k, hk‚ü© =
      œÜ.natTrans.app ‚ü®k, hk.trans hi‚ü© := rfl

namespace Hom

lemma congr_app (œÜ œÜ' : iter‚ÇÅ ‚ü∂ iter‚ÇÇ) (i : J) (hi : i ‚â§ j) :
    œÜ.natTrans.app ‚ü®i, hi‚ü© = œÜ'.natTrans.app ‚ü®i, hi‚ü© := by
  obtain rfl := Subsingleton.elim œÜ œÜ'
  rfl

def mkOfZero (iter‚ÇÅ iter‚ÇÇ : Iteration Œµ (‚ä• : J)) : iter‚ÇÅ ‚ü∂ iter‚ÇÇ where
  natTrans :=
    { app := fun ‚ü®i, hi‚ü© => eqToHom (by congr; simpa using hi) ‚â´ iter‚ÇÅ.isoZero.hom ‚â´
        iter‚ÇÇ.isoZero.inv ‚â´ eqToHom (by congr; symm; simpa using hi)
      naturality := fun ‚ü®i, hi‚ü© ‚ü®k, hk‚ü© _œÜ => by
        obtain rfl : i = ‚ä• := by simpa using hi
        obtain rfl : k = ‚ä• := by simpa using hk
        obtain rfl : _œÜ = ùüô _ := rfl
        simp }
  natTrans_app_succ i hi := by simp at hi

section

noncomputable def mkOfSuccNatTransApp {i : J} {iter‚ÇÅ iter‚ÇÇ : Iteration Œµ (wellOrderSucc i)}
    (hi : i < wellOrderSucc i)
    (œÜ : iter‚ÇÅ.trunc hi.le ‚ü∂ iter‚ÇÇ.trunc hi.le) (k : J) (hk : k ‚â§ wellOrderSucc i) :
    iter‚ÇÅ.F.obj ‚ü®k, hk‚ü© ‚ü∂ iter‚ÇÇ.F.obj ‚ü®k, hk‚ü© :=
  if hk' : k ‚â§ i then œÜ.natTrans.app ‚ü®k, hk'‚ü© else by
    obtain rfl : k = wellOrderSucc i := by
      obtain hk''|rfl := hk.lt_or_eq
      ¬∑ exfalso
        apply hk' (le_of_lt_wellOrderSucc hk'')
      ¬∑ rfl
    exact (iter‚ÇÅ.isoSucc i hi).hom ‚â´ whiskerRight (œÜ.natTrans.app ‚ü®i, by dsimp; rfl‚ü©) _ ‚â´ (iter‚ÇÇ.isoSucc i hi).inv

lemma mkOfSuccNatTransApp_eq_of_le {i : J} {iter‚ÇÅ iter‚ÇÇ : Iteration Œµ (wellOrderSucc i)}
    (hi : i < wellOrderSucc i)
    (œÜ : iter‚ÇÅ.trunc hi.le ‚ü∂ iter‚ÇÇ.trunc hi.le) (k : J) (hk : k ‚â§ i) :
    mkOfSuccNatTransApp hi œÜ k (hk.trans (self_le_wellOrderSucc i)) = œÜ.natTrans.app ‚ü®k, hk‚ü© :=
  dif_pos hk

@[simp]
lemma mkOfSuccNatTransApp_eq_succ {i : J} {iter‚ÇÅ iter‚ÇÇ : Iteration Œµ (wellOrderSucc i)}
    (hi : i < wellOrderSucc i)
    (œÜ : iter‚ÇÅ.trunc hi.le ‚ü∂ iter‚ÇÇ.trunc hi.le) :
    mkOfSuccNatTransApp hi œÜ (wellOrderSucc i) (by rfl) =
      (iter‚ÇÅ.isoSucc i hi).hom ‚â´ whiskerRight (œÜ.natTrans.app ‚ü®i, by dsimp; rfl‚ü©) _ ‚â´ (iter‚ÇÇ.isoSucc i hi).inv :=
  dif_neg (by aesop)

@[simps]
noncomputable def mkOfSuccNatTrans {i : J} {iter‚ÇÅ iter‚ÇÇ : Iteration Œµ (wellOrderSucc i)}
    (hi : i < wellOrderSucc i)
    (œÜ : iter‚ÇÅ.trunc hi.le ‚ü∂ iter‚ÇÇ.trunc hi.le) :
    iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F where
  app := fun ‚ü®k, hk‚ü© => mkOfSuccNatTransApp hi œÜ k hk
  naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© f => by
    dsimp at hk‚ÇÅ hk‚ÇÇ ‚ä¢
    have hk : k‚ÇÅ ‚â§ k‚ÇÇ := leOfHom f
    obtain h‚ÇÇ|rfl := hk‚ÇÇ.lt_or_eq
    ¬∑ replace h‚ÇÇ : k‚ÇÇ ‚â§ i := le_of_lt_wellOrderSucc h‚ÇÇ
      rw [mkOfSuccNatTransApp_eq_of_le hi œÜ k‚ÇÇ h‚ÇÇ,
        mkOfSuccNatTransApp_eq_of_le hi œÜ k‚ÇÅ (hk.trans h‚ÇÇ)]
      exact natTrans_naturality œÜ k‚ÇÅ k‚ÇÇ hk h‚ÇÇ
    ¬∑ obtain h‚ÇÅ|rfl := hk.lt_or_eq
      ¬∑ have h‚ÇÇ : k‚ÇÅ ‚â§ i := le_of_lt_wellOrderSucc h‚ÇÅ
        let f‚ÇÅ : (‚ü®k‚ÇÅ, hk‚ü© : { l | l ‚â§ wellOrderSucc i}) ‚ü∂
          ‚ü®i, self_le_wellOrderSucc i‚ü© := homOfLE h‚ÇÇ
        let f‚ÇÇ : (‚ü®i, self_le_wellOrderSucc i‚ü© : { l | l ‚â§ wellOrderSucc i}) ‚ü∂
          ‚ü®wellOrderSucc i, by dsimp; rfl‚ü© := homOfLE (self_le_wellOrderSucc i)
        obtain rfl : f = f‚ÇÅ ‚â´ f‚ÇÇ := rfl
        rw [Functor.map_comp, Functor.map_comp, assoc,
          mkOfSuccNatTransApp_eq_of_le hi œÜ k‚ÇÅ h‚ÇÇ]
        erw [‚Üê natTrans_naturality_assoc œÜ k‚ÇÅ i h‚ÇÇ (by rfl)]
        rw [mkOfSuccNatTransApp_eq_succ]
        dsimp
        change _ ‚â´ iter‚ÇÅ.mapSucc i hi ‚â´ _ = _ ‚â´ _ ‚â´ iter‚ÇÇ.mapSucc i hi
        rw [iter‚ÇÅ.mapSucc_eq i hi, iter‚ÇÇ.mapSucc_eq i hi, assoc,
          Iso.inv_hom_id_assoc]
        congr 1; simp only [‚Üê assoc]; congr 1
        ext X
        exact (Œµ.naturality _).symm
      ¬∑ obtain rfl : f = ùüô _ := rfl
        rw [Functor.map_id, Functor.map_id, id_comp, comp_id]

end

noncomputable def mkOfSucc {i : J} (iter‚ÇÅ iter‚ÇÇ : Iteration Œµ (wellOrderSucc i)) (hi : i < wellOrderSucc i)
    (œÜ : iter‚ÇÅ.trunc hi.le ‚ü∂ iter‚ÇÇ.trunc hi.le) :
    iter‚ÇÅ ‚ü∂ iter‚ÇÇ where
  natTrans := mkOfSuccNatTrans hi œÜ
  natTrans_app_zero := by
    dsimp
    rw [mkOfSuccNatTransApp_eq_of_le _ _ _ bot_le, œÜ.natTrans_app_zero]
    rfl
  natTrans_app_succ k hk := by
    obtain hk'|rfl := (le_of_lt_wellOrderSucc hk).lt_or_eq
    ¬∑ dsimp
      rw [mkOfSuccNatTransApp_eq_of_le hi œÜ k hk'.le,
        mkOfSuccNatTransApp_eq_of_le hi œÜ (wellOrderSucc k) (wellOrderSucc_le hk'),
        œÜ.natTrans_app_succ _ hk']
      rfl
    ¬∑ simp [mkOfSuccNatTransApp_eq_of_le hi œÜ k (by rfl)]

section

variable [IsWellOrderLimitElement j] (œÜ : ‚àÄ (i : J) (hi : i < j), iter‚ÇÅ.trunc hi.le ‚ü∂ iter‚ÇÇ.trunc hi.le)

def mkOfLimitNatTransApp (i : J) (hi : i ‚â§ j) : iter‚ÇÅ.F.obj ‚ü®i, hi‚ü© ‚ü∂ iter‚ÇÇ.F.obj ‚ü®i, hi‚ü© :=
  if h : i < j
    then
      (œÜ i h).natTrans.app ‚ü®i, by dsimp; rfl‚ü©
    else by
      obtain rfl : i = j := by
        obtain h'|rfl := hi.lt_or_eq
        ¬∑ exfalso
          exact h h'
        ¬∑ rfl
      exact (iter‚ÇÅ.isColimit i (show i ‚â§ i by rfl)).desc (Cocone.mk _
        { app := fun ‚ü®k, hk‚ü© => (œÜ k hk).natTrans.app ‚ü®k, by dsimp; rfl‚ü© ‚â´
            iter‚ÇÇ.F.map (homOfLE (by exact hk.le))
          naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© f => by
            have hf : k‚ÇÅ ‚â§ k‚ÇÇ := by simpa using leOfHom f
            dsimp
            rw [comp_id, congr_app (œÜ k‚ÇÅ hk‚ÇÅ) ((truncFunctor Œµ hf).map (œÜ k‚ÇÇ hk‚ÇÇ))]
            erw [natTrans_naturality_assoc (œÜ k‚ÇÇ hk‚ÇÇ) k‚ÇÅ k‚ÇÇ hf (by rfl)]
            dsimp
            rw [‚Üê Functor.map_comp, homOfLE_comp] })

lemma mkOfLimitNatTransApp_eq_of_lt (i : J) (hi : i < j) :
    mkOfLimitNatTransApp œÜ i hi.le = (œÜ i hi).natTrans.app ‚ü®i, by dsimp; rfl‚ü© := dif_pos hi

lemma mkOfLimitNatTransApp_naturality_top (i : J) (hi : i < j) :
    iter‚ÇÅ.F.map (homOfLE (by simpa using hi.le) : ‚ü®i, hi.le‚ü© ‚ü∂ ‚ü®j, by dsimp; rfl‚ü©) ‚â´ mkOfLimitNatTransApp œÜ j (by rfl) =
      mkOfLimitNatTransApp œÜ i hi.le ‚â´ iter‚ÇÇ.F.map (homOfLE (by simpa using hi.le)) := by
  rw [mkOfLimitNatTransApp_eq_of_lt œÜ i hi]
  dsimp [mkOfLimitNatTransApp]
  rw [dif_neg (by simp)]
  exact (iter‚ÇÅ.isColimit j (by rfl)).fac _ ‚ü®i, hi‚ü©

@[simps]
def mkOfLimitNatTrans : iter‚ÇÅ.F ‚ü∂ iter‚ÇÇ.F where
  app := fun ‚ü®k, hk‚ü© => mkOfLimitNatTransApp œÜ k hk
  naturality := fun ‚ü®k‚ÇÅ, hk‚ÇÅ‚ü© ‚ü®k‚ÇÇ, hk‚ÇÇ‚ü© f => by
    dsimp at hk‚ÇÅ hk‚ÇÇ
    have hk : k‚ÇÅ ‚â§ k‚ÇÇ := leOfHom f
    obtain h‚ÇÇ|rfl := hk‚ÇÇ.lt_or_eq
    ¬∑ dsimp
      rw [mkOfLimitNatTransApp_eq_of_lt _ k‚ÇÇ h‚ÇÇ,
        mkOfLimitNatTransApp_eq_of_lt _ k‚ÇÅ (lt_of_le_of_lt hk h‚ÇÇ),
        congr_app (œÜ k‚ÇÅ (lt_of_le_of_lt hk h‚ÇÇ)) ((truncFunctor Œµ hk).map (œÜ k‚ÇÇ h‚ÇÇ))]
      exact natTrans_naturality (œÜ k‚ÇÇ h‚ÇÇ) k‚ÇÅ k‚ÇÇ hk (by rfl)
    ¬∑ obtain h‚ÇÅ|rfl := hk‚ÇÅ.lt_or_eq
      ¬∑ exact mkOfLimitNatTransApp_naturality_top _ _ h‚ÇÅ
      ¬∑ obtain rfl : f = ùüô _ := rfl
        dsimp
        simp only [map_id, id_comp, comp_id]

def mkOfLimit  :
    iter‚ÇÅ ‚ü∂ iter‚ÇÇ where
  natTrans := mkOfLimitNatTrans œÜ
  natTrans_app_zero := by simp [mkOfLimitNatTransApp_eq_of_lt œÜ ‚ä•
    (IsWellOrderLimitElement.bot_lt j)]
  natTrans_app_succ i h := by
    dsimp
    have h' := IsWellOrderLimitElement.wellOrderSucc_lt h
    rw [mkOfLimitNatTransApp_eq_of_lt œÜ _ h',
      (œÜ _ h').natTrans_app_succ i (self_lt_wellOrderSucc h),
      mkOfLimitNatTransApp_eq_of_lt œÜ _ h,
      congr_app (œÜ i h) ((truncFunctor Œµ (self_le_wellOrderSucc i)).map (œÜ (wellOrderSucc i) h'))]
    rfl

end

end Hom

variable (iter‚ÇÅ iter‚ÇÇ) in
lemma nonempty_hom : Nonempty (iter‚ÇÅ ‚ü∂ iter‚ÇÇ) := by
  let P := fun (i : J) => ‚àÄ (hi : i ‚â§ j),
    Nonempty ((truncFunctor Œµ hi).obj iter‚ÇÅ ‚ü∂ (truncFunctor Œµ hi).obj iter‚ÇÇ)
  suffices ‚àÄ i, P i from this j (by rfl)
  refine fun _ => WellFoundedLT.induction _ (fun i hi hi' => ?_)
  obtain rfl|‚ü®i, rfl, hi''‚ü©|_ := eq_bot_or_eq_succ_or_isWellOrderLimitElement i
  ¬∑ exact ‚ü®Hom.mkOfZero _ _‚ü©
  ¬∑ exact ‚ü®Hom.mkOfSucc _ _ hi'' ((hi i hi'' (hi''.le.trans hi')).some)‚ü©
  ¬∑ exact ‚ü®Hom.mkOfLimit (fun k hk => (hi k hk (hk.le.trans hi')).some)‚ü©

noncomputable instance : Unique (iter‚ÇÅ ‚ü∂ iter‚ÇÇ) :=
  uniqueOfSubsingleton (Nonempty.some (nonempty_hom _ _))

noncomputable def iso : iter‚ÇÅ ‚âÖ iter‚ÇÇ where
  hom := default
  inv := default

end Iteration

end Functor

end CategoryTheory
